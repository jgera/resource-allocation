package allocation

import allocation.*
import allocation.facts.*
import allocation.actions.*
import allocation.agents.*
import uk.ac.imperial.presage2.core.IntegerTime
import java.lang.Math

global org.apache.log4j.Logger logger

declare Processed
	action : TimestampedAction
end

declare Appropriated
	agent : Agent
	round : int
	quantity : double
end

rule "Max level"
	salience -11
	when
		$pool : CommonPool( resourceLevel > maxLevel, $max : maxLevel)
	then
		modify($pool) {
			setResourceLevel($max);
		}
		logger.info("Pool was overfilled, reseting to max level");
end

rule "Refill pool"
	salience -10
	when
		$t : IntegerTime()
		$pool : CommonPool( $t.intValue() > getLastFilled(), state == Phase.Exclude )
	then
		double fillAmount = $pool.getMaxLevel() / 2;
		int time = $t.intValue();
		if(time < 50) {
			fillAmount *= 1;
		} else if(time < 100) {
			fillAmount *= 0.95;
		} else if(time < 150) {
			fillAmount *= 0.87;
		} else if(time < 200) {
			fillAmount *= 0.52;
		} else if(time < 250) {
			fillAmount *= 0.92;
		} else if(time < 300) {
			fillAmount *= 0.97;
		} else if(time < 350) {
			fillAmount *= 0.62;
		} else if(time < 400) {
			fillAmount *= 0.90;
		} else if(time < 450) {
			fillAmount *= 0.50;
		} else {
			fillAmount *= 0.91;
		}
		modify($pool) {
			setResourceLevel( $pool.getResourceLevel() + fillAmount),
			setLastFilled( time )
		}
		logger.info("Fill resource by "+ fillAmount +", pool resource level is "+ $pool.getResourceLevel() );
end

rule "Take out of pool"
	when
		$a : Appropriate( $pid : pool, $ag : player, $round : round )
		not( Appropriated( agent == $ag, round == $round ) )
		not( exists( Processed( $a ;) ) )
		$pool : CommonPool( id == $pid, state == Phase.Appropriate, round == $round )
		Agent( this == $ag, getPool() == $pid )
	then
		insert( new Appropriated( $ag, $round, Math.min( $a.amount, $pool.getResourceLevel() ) ) );
		modify($pool) {
			setResourceLevel( $pool.getResourceLevel() - $a.amount )
		}
		insert( new Processed( $a ) );
		logger.info($a);
end

rule "End of the world"
	when
		$pool : CommonPool( resourceLevel < 0 )
	then
		retract($pool);
		logger.info("DEPLEATED!!");
end

rule "Prune Appropriate"
	when
		CommonPool( $round : round, $id : id )
		$a : Appropriate( pool == $id, round < $round )
	then
		retract($a);
		//logger.info("Prune "+ $a);
end

rule "Prune Processed"
	when
		$p : Processed($a : action)
		not(exists(TimestampedAction(this == $p.action)))
	then
		retract( $p );
end

