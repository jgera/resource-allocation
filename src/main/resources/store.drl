package allocation

import allocation.*
import allocation.facts.*
import allocation.actions.*
import allocation.newagents.*
import java.lang.Double
import java.lang.Integer
import java.util.List

global org.apache.log4j.Logger logger
global uk.ac.imperial.presage2.core.db.StorageService sto

declare negResourceHandler
	round : int
	quantity : double
	pool : CommonPool
end

rule "Log pool level after appropriation"
	salience -900
	ruleflow-group 'exclude'
	when
		$pool : CommonPool($pid : id, $level : resourceLevel)
		Institution($round : round, pool == $pool)
	then
		logger.info("Pool level: "+ $level);
		if ($level >= 0){
			sto.getSimulation().getEnvironment().setProperty("level", $round, Double.toString($level));
		}
		else{
			sto.getSimulation().getEnvironment().setProperty("level", $round, Double.toString(0));
			insert( new negResourceHandler($round,$level,$pool));
		}
end

rule "Log sum of appropriations"
	salience -1000
	ruleflow-group 'exclude'
	when
		Institution($round : round, $pool : pool)
		$appropriations :  List() from collect ( Appropriated(pool == $pool, round == $round))
		not (exists negResourceHandler(round == $round, pool == $pool))
	then
		double appSum = 0;
		for (Object a : $appropriations){
			Appropriated app = (Appropriated) a;
			appSum += app.getQuantity();
		}
		logger.info("Sum of appropriation: "+ appSum);
		sto.getSimulation().getEnvironment().setProperty("appropriation sum", $round, Double.toString(appSum));
end

rule "Log sum of appropriations when gone bankrupt"//agents can't appropriate negative amount!!
	salience -1000
	ruleflow-group 'exclude'
	when
		Institution($round : round, $pool : pool)
		$appropriations :  List() from collect ( Appropriated(round == $round, pool == $pool))
		negResourceHandler(round == $round, $level : quantity, pool == $pool)
	then
		double appSum = 0;
		for (Object a : $appropriations){
			Appropriated app = (Appropriated) a;
			appSum += app.getQuantity();
		}
		appSum += $level; //negative resource
		logger.info("Sum of appropriation: "+ appSum);
		sto.getSimulation().getEnvironment().setProperty("appropriation sum", $round, Double.toString(appSum));
end

rule "Log active members" //activeMemberCount is value start only
	salience -1000
	ruleflow-group 'exclude'
	when
		Institution($iid : id, $round : round, $pool : pool)
		$members :  List() from collect ( Member(institutionId == $iid, active == true))
	then
		logger.info("Active members: "+ $members.size());
		sto.getSimulation().getEnvironment().setProperty("active members", $round, Integer.toString($members.size()));
end

rule "log whether institution is alive or dead" //for averaging purposes
	salience -1000
	ruleflow-group 'exclude'
	when
		$t : IntegerTime( )
		$institutions : List() from collect ( Institution(round == $t.intValue()) )
	then
		logger.info("Live institutions "+ $institutions.size());
		sto.getSimulation().getEnvironment().setProperty("live institution", $t.intValue(), Integer.toString($institutions.size()));
end
		

		